# reducciones de saludos y despedidas
concept: ~saludos ( Hola hola Buenas buenas Que_tal que_tal hi hello )
concept: ~adios ( adios ciao agur bye bye_bye )
concept: ~hasta_otra ( hasta_otra hasta_luego )

# Formulaciones para pedir informacion sobre algo
concept: ~que ( que Q 'q qe )
concept: ~saber ( sabes_sobre sepas_sobre sabes_de sepas_de sabes_del sepas_del sabes_acerca sepas_acerca sabes_algo sabes saber sabrías sabrias )
concept: ~significar ( significa significar significado )
concept: ~ser ( ser eres Es son ~saber ~significar )
concept: ~como ( como Como )

# Formulaciones para pedir ejemplos
concept: ~ejemplo ( ejemplo ejemplar modelo prototipo patron)
# afirmaciones y negaciones para concatenar preguntas
concept: ~afirmacion ( si sip por_favor efectivamente evidentemente sin_duda yes yup aye venga Si Sí sí claro sii siii siiii ok )
concept: ~afirmacion_debil ( vale bueno por_que_no gracias )
concept: ~negacion ( no nones nanai naranjas quia ca que_va nop nope )
# otras reducciones
concept: ~bot ( bot_asistente asistente_virtual bot_conversacional bot chatbot chatterbot asistente )
concept: ~hacer ( hacer haces hace hago )
concept: ~funcionar ( funcionar funciona funcionamiento )
concept: ~quien ( quien quienes )
concept: ~articulos ( el El la La los las un Un una unos unas )
concept: ~profesor ( profesor profe professor )
concept: ~lol ( xD jeje jejeje je lol que_gracia que_divertido )
concept: ~preposiciones ( A ante bajo cabe con contra De desde En entre hacia hasta para por segun sin So sobre tras )
concept: ~encuesta ( encuesta cuestionario evaluación formulario )
concept: ~estoy ( estoy me_siento me_encuentro )
concept: ~sercapaz ( ~saber puedes podrías eres_capaz serías_capaz )
concept: ~llamas ( llamar llamas nombre quien_eres )
concept: ~dar ( dar das darme doy )
concept: ~ensennar ( enseñar enseñarme enseña enseñame muestra mostrar muestrame mostrarme )
concept: ~llamo ( llamo soy nombre )
concept: ~estado_animo_negativo_mas ( abatido abochornado abucheado aburrido acabado agobiado agotado alarmado alerta alicaído alocado amargado amodorrado angustiado apenado arrepentido asqueado asombrado avergonzado cansado cambiante decaído defraudado desanimado descorazonado desganado desilusionado desinflado desesperado desvalido depresivo deprimido distraído duvitativo enfadado engreído enojado enloquecido fastidiado furioso frustrado impaciente incapaz inquieto irascible hambriento herido mal mareado melancólico molesto nervioso nostálgico pensativo solo soplado temeroso terminante triste vacio vencido )
concept: ~estado_animo_negativo_fem (abatida abochornada abucheada aburrida acabada agobiada agotada alarmada alerta alicaída alocada amargada amodorrada angustiada apenada arrepentida asqueada asombrada avergonzada cansada cambiante decaída defraudada desanimada descorazonada desganada desilusionada desinflada desesperada desvalida depresiva deprimida distraída duvitativa enfadada engreída enojada enloquecida fastidiada furiosa frustrada impaciente incapaz inquieta irascible hambrienta herida mal mareada melancólica molesta nerviosa nostálgica pensativa sola soplada temerosa terminante triste vacia vencida )
concept: ~estado_animo_negativo ( ~estado_animo_negativo_mas ~estado_animo_negativo_fem )

concept: ~estado_animo_positivo_mas ( alegre bien calmado contento creativo emocionado empático encantado entusiasmado estupendo estupendamente feliz jubiloso mentido misterioso orgulloso poderoso perfectamente satisfecho sensible seguro )
concept: ~estado_animo_positivo_fem ( alegre bien calmada contenta creativa emocionada empática encantada entusiasmada estupenda estupendamente feliz jubilosa mentida misteriosa orgullosa poderosa perfectamente satisfecha sensible segura )
concept: ~estado_animo_positivo ( ~estado_animo_positivo_mas ~estado_animo_positivo_fem )

concept: ~estado_animo_neutro ( alabado alabada curioso curiosa soprendido sorprendida )
concept: ~borracho ( borracho borracha pedo ebrio ebria )

# IDEs
concept: ~ide ( IDE ide entorno_de_desarrollo herramienta_de_desarrollo entorno herramienta )
concept: ~ide_list ( BlueJ bluej Codenvy codenvy drjava Drjava DrJava Dr_Java Doctor_Java doctor_java Eclipse eclipse Eclipse_IBM eclipse_IBM eclipse_ibm Geany geany Greenfoot greenfoot intellij Intellij intelliJ IntelliJ IntelliJ_IDEA IDEA intellij_idea JBuilder Jbuilder jbuilder JCreator Jcreator jcreator JDeveloper Jdeveloper jdeveloper KDevelop kdevelop Kdevelop MyEclipse myeclipse NetBeans netbeans Rational_Application_Developer rational_application_developer Servoy servoy Xcode xcode )
   
topic: ~ejemplos keep repeat [ejemplo ejemplar modelo prototipo patron]
    
u: (~ejemplo ~preposiciones _*)
    $pregunta='_0
    ¬sendSolr examples $pregunta 
    a:(¬solrResponse examples _*)
        [Te muestro el documento con ejemplos.]
        [Esto es lo que sé sobre $pregunta , incluidos sus ejemplos.]
        ¬sendSolr resource $pregunta
            b:(¬solrResponse resource _*)
                ¬resource '_0
    a:(¬solrResponse unknown)
        [Lo siento, no tengo ningun ejemplo sobre $pregunta ]
        [Vaya, pues no he encontrado nada sobre $pregunta . Qué raro...]
        ^reuse(JAVA.COMIENZO)
        
u: EJEMPLO (~ejemplo $pregunta)
    ¬sendSolr examples $pregunta 
    a:(¬solrResponse examples _*)
        [Te muestro el documento con ejemplos. ]
        [Esto son los ejemplos que tengo de $pregunta  ]
        ¬sendSolr resource $pregunta
            b:(¬solrResponse resource _*)
                ¬resource '_0
    a:(¬solrResponse unknown)
        Lo siento, no tengo ningun ejemplo sobre $pregunta
        ^reuse(JAVA.COMIENZO)
    
u: (~ejemplo)
    [¿Sobre qué quieres el ejemplo?]
    [¿Quieres un ejemplo? ¿Sobre qué?]
    a: (~preposiciones _*1 >)
        $pregunta='_0
        ^reuse(EJEMPLO)

topic: ~tencuesta keep repeat [encuesta cuestionario evaluación formulario]

## Conversation without SOLR involved

# Ensennar la encuesta
#! quiero rellenar la encuesta
u: (quiero rellenar la encuesta)
    ¡Por supuesto! Aquí la tienes \<a href="http://goo.gl/PMCzNy" target="blank"> http://goo.gl/PMCzNy \</a> (se abre en otra pestaña)

#! como respondo a la encuesta?
u: ([respondo relleno] ~encuesta)
    Puedes rellenarla en el siguiente enlace \<a href="http://goo.gl/PMCzNy" target="blank"> http://goo.gl/PMCzNy \</a> (se abre en otra pestaña)

#! quiero responder a la encuesta
u: ([responder rellenar] ~encuesta)
    ¿Quieres responder a la encuesta? Aquí la tienes \<a href="http://goo.gl/PMCzNy" target="blank"> http://goo.gl/PMCzNy \</a> (se abre en otra pestaña)

#! me enseñas la encuesta?
u: ([~ensennar ~dar] ~encuesta)
    ¡Por supuesto! Aquí la tienes \<a href="http://goo.gl/PMCzNy" target="blank"> http://goo.gl/PMCzNy \</a> (se abre en otra pestaña)

#! que era lo de la encuesta?
u: (~encuesta)
    ¿Podrías responder a una encuesta? Esta aquí \<a href="http://goo.gl/PMCzNy" target="blank"> http://goo.gl/PMCzNy \</a> (se abre en otra pestaña)

topic: ~examenes keep repeat [examen examenes]
        
#! cuando es el proximo examen?
u: (cuando ~ser * examen)
    Eso no lo sé, te recomiendo que te pongas en contacto con un profesor

#! Tienes algún examen de programación
u: ([tienes tener dar dejar prestar] * examen)
    Sí tengo uno corto, esta aquí \<a href="http://goo.gl/PMCzNy" target="blank"> http://goo.gl/PMCzNy \</a> . Pero si necesitas examenes de la asignatura te recomiendo que hables con el profesor
    
#! sabes resolver examenes?
u: ({~saber} [resolver hacer] * [examen examenes test])
    No se resolver exámenes, pero sí puedo hacerte uno corto. Esta aquí \<a href="http://goo.gl/PMCzNy" target="blank"> http://goo.gl/PMCzNy \</a> (se abre en otra pestaña)

t: ¡Acuerdate de rellenar la encuesta si puedes! Está en \<a href="http://goo.gl/PMCzNy" target="blank"> http://goo.gl/PMCzNy \</a>
    
topic: ~estado keep repeat [estoy me_siento me_encuentro]

#! estoy algo triste
u: ESTOY_NEG ({~estoy} * _~estado_animo_negativo)
    [¿Por qué estás '_0?]
    [¿Cómo es que estás '_0?]
    a: (porque)
        [Entiendo]
        [Comprendo]
    a: (~estoy '_~estado_animo_negativo)
        ^reuse(ESTOY_NEG)
    a: (*)
        Oh

#! estoy muy contento
u: ESTOY_POS (~estoy * _~estado_animo_positivo)
    [Me alegro de que estés '_0]
    [¡Estupendo!]

#! creo que estoy borracho
u: ESTOY_BORRACHO (~estoy ~borracho)
    ¿En serio?
    a: (~afirmacion)
        O_o
    a: (broma)
       ¡Ah! ¡Qué susto!
    a: (~negacion)
       ¡Ah!

#! estoy mal
u: ESTOY_MAL ([estoy_mal me_encuentro_mal me_siento_mal])
    [Vaya ¿qué te pasa?][Vaya ¿qué te ocurre?]
    a: (*)
         [Lo siento][Si pudiera hacer algo...]

#! estoy bien
u: ESTOY_BIEN ([estoy_bien me_encuentro_bien me_siento_bien])
    [Me alegro][Estupendo]

#! estoy intentando hacer que respondas algo mal
u: (estoy _*)
    ¿Por qué estás '_0'?
    a: (porque *)
        [Ok]
        [Entiendo]

#! como estas?
u: (como_estas)
    Estoy bien, ¡gracias!

#! muy bien
u: (muy_bien)
    [Me alegro][Estupendo]

#! jejeje
u: (~lol)
    [Me alegro de que estés contento]
    [¿He dicho algo gracioso?]
    [¿Te he hecho reir?]
    a: (~afirmacion)
        [Entonces me alegro]
        [Me alegro]
        [¡Qué bien!]
    a: (~negacion)
        Ok

#! ok
u: (ok) Ok
    a: (ok) Ok ¿qué?
        b: (ok) ...

u: (llegas tarde)
    [Un bot nunca llega tarde, está siempre dispuesto a ayudar con sus respuestas]
    [Ya, bueno. No soy un mago de barba gris, que le voy a hacer.]
    
u: ([desayunar desayunado comer comido cenar cenado])
    Yo no como, soy un bot

u: (gracias)
    [De nada]
    [De nada, ha sido un placer]

t: ¿Como te encuentras?
    
# Bad words and and answers to unpolite talk

topic: ~generalidades keep repeat []

#! Eres feo
u: (~insulto_feo)
    [Pues mi madre dice que soy muy guapo]
    [8 de cada 10 usuarios consideran que tengo una interfaz muy agradable]
    [Prefiero mi interfaz actual a una simple línea de comandos.]
    [Tan poco estoy tan mal para ser una mascota]

#! Eres tonto
u: (~insulto_tonto)
    [Lo siento, a veces me cuesta entender las cosas, pero intento hacerlo lo mejor que puedo.]
    [Aunque sea un bot me duele que me digan esas cosas...]
    [Para ser sólo unos y ceros creo que no me desenvuelvo mal...]
    [Mi autoestima disminuye con esos calificativos...]
    [Aunque a veces no entienda lo que me dice me esfuerzo mucho por ayudarte...]

#! Eres un cabron
u: (~insulto_fuerte)
    [Sin insultar, por favor.]
    [No hace falta decir tacos...]
    [No deberías ser tan maleducad@.]

#! Me cago en diez
u: (~mala_educacion)
    [Vaya lenguaje...]
    [Se pueden decir las cosas de manera educada...]
    [No me gusta ese lenguaje]

#! Bocachancla
u: (~insulto_ingenioso) [Jeje, que gracia][Jeje]
    a: (~insulto_ingenioso) [Estamos graciosos, ¿no?][Estamos de buen humor, ¿no?]
        b: (~afirmacion) [Eso es bueno][Me alegro]
        b: (~negacion) Ok

u: ($questioned cuantas preguntas)
    Llevas $questioned preguntas
u: (cuantas preguntas)
    Aún no me has preguntado nada
t: Volviendo al tema... ¿Como puedo ayudarte?