# reducciones de saludos y despedidas
concept: ~saludos [Hola hola Buenas buenas Que_tal que_tal]
concept: ~adios [adios ciao agur bye bye_bye ]
concept: ~hasta_otra [hasta_otra hasta_luego]
# Formulaciones para pedir informacion sobre algo
concept: ~que [que Q 'q qe]
concept: ~saber [sabes_sobre sepas_sobre sabes_de sepas_de sabes_del sepas_del sabes_acerca sepas_acerca sabes_algo]
concept: ~ser [ser eres Es son ~saber]
# Formulaciones para pedir ejemplos
concept: ~ejemplo [ejemplo ejemplar modelo prototipo patron]
# adirmaciones y negaciones para concatenar preguntas
concept: ~afirmacion [si sip por_favor efectivamente evidentemente sin_duda yes yup aye venga]
concept: ~afirmacion_debil [vale bueno por_que_no gracias]
concept: ~negacion [no nones nanai naranjas quia ca que_va nop nope]
# otras reducciones
concept: ~hacer [hacer haces hace]
concept: ~quien [quien quienes]
concept: ~articulos [El La los las Un una unos unas]
concept: ~profesor [profesor profe professor]
concept: ~preposiciones [A ante bajo cabe con contra De desde En entre hacia hasta para por segun sin So sobre tras]

topic: ~preguntas keep repeat []


u: (~saludos)
    [Hola, soy Duke y estoy aqui para ayudarte.][Hola, ¿necesitas ayuda?][¡Hola!]

u: (buenos_dias)
    [Hola, buenos días][Buenos días]

u: (~adios *)
    [¡Que tengas buen día!][Adiós ¡Gracias!]

u: (~hasta_otra *)
    Hasta luego, vuelve cuando quieras

u: (tengas * buen_dia)
    Tú también, ¡gracias!

u: (muy_bien)
    [Me alegro][Estupendo]

# Simple, generic questions

u: (~ser {~articulos} ~profesor)
    No, soy un bot asistente para ayudarte a encontrar respuestas.
    # TODO: Add rejoinder to offer contact info.

u: (~quien ~ser)
    Me llamo Duke y soy un bot asistente para ayudarte con tus preguntas.

u: (~que < * ~hacer)
    [Mi funcion es ayudarte a encontrar respuestas][Busco información en mis documentos de Java]



t: Hola, en que puedo ayudarte?

# Generic questions
#! que es un for?

u: COMIENZO (~que ~ser {~articulos} * > _*-1 >) # This semi-weird pattern will return the last word of the sentence
    $pregunta='_0
    ¬sendSolr definition $pregunta
    ¬label $pregunta
    
    # Process the maia responses
    a: (¬solrResponse definition)
        [Te enseño la información que tengo sobre $pregunta][Esto es lo que se sobre $pregunta] ¬sendSolr resource $pregunta
        
        # Ask for the link to show in the frame
        b: (¬solrResponse resource _*)
            ¬resource '_0 _ ¬sendSolr links $pregunta
            c: (¬solrResponse links _*)
                ¬solrLinks '_0 _
            c:(¬solrResponse unknown)
                \@fullResponse
        b: () # No matter if there is a link or not, ask for related
            ¬sendSolr links $pregunta
            c: (¬solrResponse links _*)
                Tambien puedes preguntar sobre '_0 \@full\_response
            c:(¬solrResponse unknown)
                \@fullResponse
        
    a: (¬solrResponse unknown)
        ^reuse(GAMBIT)
    
u: GAMBIT (_~javaconcept)
    $pregunta = '_0
    Lo siento, no te he entendido bien. ¬gambit $pregunta
    # Busco lo más aproximado en solr.
    a: (¬gambit response _*1)
        $$pregunta_gambit='_0
        Te refieres a $$pregunta_gambit ?
        b: (~afirmacion)
            $pregunta=$$pregunta_gambit
            ^reuse(COMIENZO)
        b: (~negacion)
            Me temo que no puedo ayudarte. Por que no me preguntas sobre alguna otra cosa?
    a:(¬gambitUnknown)
        ¿ Podrías formularlo de alguna otra manera?


u: (~ejemplo ~preposiciones _*)
    $pregunta='_0
    ¬sendSolr examples $pregunta 
    a:(solrResponse examples _*)
        Te muestro el documento con ejemplos. ¬sendSolr resource $pregunta
            b:(¬solrResponse resource _*)
                ¬resource '_0
    a:(¬solrResponse unknown)
        Lo siento, no tengo ningun ejemplo sobre $pregunta
        
u: EJEMPLO (~ejemplo $pregunta)
    ¬sendSolr examples $pregunta 
    a:(¬solrResponse examples _*)
        Te muestro el documento con ejemplos. ¬sendSolr resource $pregunta
            b:(¬solrResponse resource _*)
                ¬resource '_0
    a:(¬solrResponse unknown)
        Lo siento, no tengo ningun ejemplo sobre $pregunta
    
u: (~ejemplo)
    Sobre que quieres el ejemplo?
    a: (~preposiciones _*1 >)
        $pregunta='_0
        ^reuse(EJEMPLO)

